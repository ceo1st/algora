defmodule Algora.Settings do
  @moduledoc false
  use Ecto.Schema

  alias Algora.Accounts
  alias Algora.Repo

  @primary_key {:key, :string, []}
  schema "settings" do
    field :value, :map
    timestamps()
  end

  def get(key) do
    case Repo.get(__MODULE__, key) do
      nil -> nil
      config -> config.value
    end
  end

  def set(key, value) do
    %__MODULE__{}
    |> Ecto.Changeset.cast(%{key: key, value: value}, [:key, :value])
    |> Ecto.Changeset.validate_required([:key, :value])
    |> Repo.insert(on_conflict: {:replace, [:value]}, conflict_target: :key)
  end

  def set!(key, value) do
    case set(key, value) do
      {:ok, _} -> :ok
      {:error, reason} -> raise "Failed to set #{key} to #{value}: #{reason}"
    end
  end

  def delete(key) do
    case Repo.get(__MODULE__, key) do
      nil -> {:ok, nil}
      config -> Repo.delete(config)
    end
  end

  def get_featured_developers do
    case get("featured_developers") do
      %{"handles" => handles} when is_list(handles) -> handles
      _ -> nil
    end
  end

  def set_featured_developers(handles) when is_list(handles) do
    set("featured_developers", %{"handles" => handles})
  end

  def get_featured_orgs do
    case get("featured_orgs") do
      %{"handles" => handles} when is_list(handles) -> handles
      _ -> []
    end
  end

  def set_featured_orgs(handles) when is_list(handles) do
    set("featured_orgs", %{"handles" => handles})
  end

  def get_featured_collabs do
    case get("featured_collabs") do
      %{"handles" => handles} when is_list(handles) -> handles
      _ -> nil
    end
  end

  def set_featured_collabs(handles) when is_list(handles) do
    set("featured_collabs", %{"handles" => handles})
  end

  def set_user_profile(handle, profile) do
    set("user_profile:#{handle}", profile)
  end

  def get_user_profile(handle) do
    get("user_profile:#{handle}")
  end

  def get_org_matches(org) do
    if get_user_profile(org.handle) do
      []
    else
      case get("org_matches:#{org.handle}") do
        %{"matches" => matches} when is_list(matches) ->
          load_matches(matches)

        _ ->
          if tech_stack = List.first(org.tech_stack) do
            get_tech_matches(tech_stack)
          else
            []
          end
      end
    end
  end

  def set_org_matches(org_handle, matches) when is_binary(org_handle) and is_list(matches) do
    set("org_matches:#{org_handle}", %{"matches" => matches})
  end

  def get_job_matches(job, opts \\ []) do
    opts = Keyword.put_new(opts, :limit, 1000)

    case get("job_matches:#{job.id}") do
      %{"matches_2" => matches} when is_list(matches) ->
        matches
        |> Enum.map(fn %{"user_id" => id} -> %{user_id: id} end)
        |> load_matches_2()

      %{"matches" => matches} when is_list(matches) ->
        matches
        |> load_matches()
        |> Enum.take(opts[:limit])

      _ ->
        matches =
          [
            tech_stack: job.tech_stack,
            email_required: false,
            sort_by:
              case get_job_criteria(job) do
                criteria when map_size(criteria) > 0 -> criteria
                _ -> [{"solver", true}]
              end
          ]
          |> Keyword.merge(opts)
          |> Algora.Cloud.list_top_matches()

        # Cache the raw matches for future calls
        _count = get_job_matches_count(job, opts)
        set_job_matches_2(job.id, matches)

        load_matches_2(matches)
    end
  end

  def set_job_matches_count(job_id, count) when is_binary(job_id) and is_integer(count) do
    set("job_matches_count:#{job_id}", %{"count" => count})
  end

  def get_job_matches_count(job, opts \\ []) do
    case get("job_matches_count:#{job.id}") do
      %{"count" => count} when is_integer(count) ->
        count

      _ ->
        count =
          case get("job_matches:#{job.id}") do
            %{"matches" => matches} when is_list(matches) ->
              length(matches)

            _ ->
              [
                tech_stack: job.tech_stack,
                email_required: false,
                sort_by:
                  case get_job_criteria(job) do
                    criteria when map_size(criteria) > 0 -> criteria
                    _ -> [{"solver", true}]
                  end
              ]
              |> Keyword.merge(opts)
              |> Algora.Cloud.count_top_matches()
          end

        set_job_matches_count(job.id, count)
        count
    end
  end

  def get_top_stargazers(job) do
    [
      job: job,
      tech_stack: job.tech_stack,
      limit: 50,
      sort_by: get_job_criteria(job)
    ]
    |> Algora.Cloud.list_top_stargazers()
    |> load_matches_2()
  end

  def set_job_criteria(job_id, criteria) when is_binary(job_id) and is_map(criteria) do
    set("job_criteria:#{job_id}", %{"criteria" => criteria})
  end

  def get_job_criteria(job) do
    cond do
      job.countries != [] ->
        %{"countries" => job.countries}

      job.regions != [] ->
        %{"regions" => job.regions}

      true ->
        case get("job_criteria:#{job.id}") do
          %{"criteria" => criteria} when is_map(criteria) -> criteria
          _ -> %{}
        end
    end
  end

  def set_job_matches(job_id, matches) when is_binary(job_id) and is_list(matches) do
    set("job_matches:#{job_id}", %{"matches" => matches})
  end

  def set_job_matches_2(job_id, matches) when is_binary(job_id) and is_list(matches) do
    set("job_matches:#{job_id}", %{"matches_2" => matches})
  end

  def get_tech_matches(tech) do
    case get("tech_matches:#{String.downcase(tech)}") do
      %{"matches" => matches} when is_list(matches) -> load_matches(matches)
      _ -> []
    end
  end

  def set_tech_matches(tech, matches) when is_binary(tech) and is_list(matches) do
    set("tech_matches:#{String.downcase(tech)}", %{"matches" => matches})
  end

  def load_matches(matches) do
    user_map =
      [handles: Enum.map(matches, & &1["handle"]), limit: :infinity]
      |> Accounts.list_developers()
      |> Enum.filter(& &1.provider_login)
      |> Map.new(fn user -> {user.handle, user} end)

    Enum.flat_map(matches, fn match ->
      if user = Map.get(user_map, match["handle"]) do
        # TODO: N+1
        profile = get_user_profile(user.handle)
        projects = Accounts.list_contributed_projects(user, limit: 2)
        avatar_url = profile["avatar_url"] || user.avatar_url
        hourly_rate = match["hourly_rate"] || profile["hourly_rate"]
        hours_per_week = match["hours_per_week"] || profile["hours_per_week"] || user.hours_per_week

        [
          %{
            user: %{user | avatar_url: avatar_url},
            projects: projects,
            badge_variant: match["badge_variant"],
            badge_text: match["badge_text"],
            hourly_rate: if(hourly_rate, do: Money.new(:USD, hourly_rate, no_fraction_if_integer: true)),
            hours_per_week: hours_per_week
          }
        ]
      else
        []
      end
    end)
  end

  def load_matches_2(matches) do
    user_map =
      [ids: Enum.map(matches, & &1[:user_id]), limit: :infinity]
      |> Accounts.list_developers()
      |> Enum.filter(& &1.provider_login)
      |> Map.new(fn user -> {user.id, user} end)

    Enum.flat_map(matches, fn match ->
      if user = Map.get(user_map, match[:user_id]) do
        [%{user: user, contribution_score: match["contribution_score"]}]
      else
        []
      end
    end)
  end

  def get_blocked_users do
    case get("blocked_users") do
      %{"handles" => handles} when is_list(handles) -> handles
      _ -> []
    end
  end

  def set_blocked_users(handles) when is_list(handles) do
    set("blocked_users", %{"handles" => handles})
  end

  def get_featured_transactions do
    case get("featured_transactions") do
      %{"ids" => ids} when is_list(ids) -> ids
      _ -> nil
    end
  end

  def set_featured_transactions(ids) when is_list(ids) do
    set("featured_transactions", %{"ids" => ids})
  end

  def get_featured_talent_for_location(state) when is_binary(state) do
    case get("featured_talent_by_location") do
      %{} = locations ->
        case String.split(state, "-*") do
          [country, ""] ->
            prefix = country <> "-"

            locations
            |> Enum.flat_map(fn {key, ids} ->
              if String.starts_with?(key, prefix) and is_list(ids), do: ids, else: []
            end)
            |> Enum.uniq()

          _ ->
            case Map.get(locations, state) do
              ids when is_list(ids) -> ids
              _ -> []
            end
        end

      _ ->
        []
    end
  end

  def set_featured_talent_for_location(state, ids) when is_binary(state) and is_list(ids) do
    locations =
      case get("featured_talent_by_location") do
        %{} = m -> m
        _ -> %{}
      end

    set("featured_talent_by_location", Map.put(locations, state, ids))
  end

  def add_featured_talent_for_location(state, user_id) when is_binary(state) and is_binary(user_id) do
    ids = get_featured_talent_for_location(state)
    if user_id in ids, do: {:ok, ids}, else: set_featured_talent_for_location(state, ids ++ [user_id])
  end

  def remove_featured_talent_for_location(state, user_id) when is_binary(state) and is_binary(user_id) do
    ids = get_featured_talent_for_location(state)
    set_featured_talent_for_location(state, Enum.reject(ids, &(&1 == user_id)))
  end

  def get_home_carousel_candidate_ids do
    case get("home_carousel_candidates") do
      %{"ids" => ids} when is_list(ids) -> ids
      _ -> nil
    end
  end

  def set_home_carousel_candidate_ids(ids) when is_list(ids) do
    set("home_carousel_candidates", %{"ids" => ids})
  end

  def get_wire_details do
    case get("wire_details") do
      %{"details" => details} when is_map(details) -> details
      _ -> nil
    end
  end

  def set_wire_details(details) when is_map(details) do
    set("wire_details", %{"details" => details})
  end

  def get_subscription_price do
    case get("subscription") do
      %{"price" => %{"amount" => _amount, "currency" => _currency} = price} ->
        Algora.MoneyUtils.deserialize(price)

      _ ->
        nil
    end
  end

  def set_subscription_price(price) do
    set("subscription", %{"price" => Algora.MoneyUtils.serialize(price)})
  end

  def get_campaign_timestamp do
    case get("campaign_timestamp") do
      %{"timestamp" => timestamp} when is_binary(timestamp) -> timestamp
      _ -> nil
    end
  end

  def set_campaign_timestamp(timestamp) when is_binary(timestamp) do
    set("campaign_timestamp", %{"timestamp" => timestamp})
  end

  def update_campaign_timestamp do
    timestamp = format_timestamp(DateTime.utc_now())
    set_campaign_timestamp(timestamp)
  end

  def get_org_members(org_handle) when is_binary(org_handle) do
    case get("org_members:#{org_handle}") do
      %{"members" => members} when is_list(members) -> members
      _ -> []
    end
  end

  def set_org_members(org_handle, members) when is_binary(org_handle) and is_list(members) do
    set("org_members:#{org_handle}", %{"members" => members})
  end

  def get_pipeline_candidates(org_handle) when is_binary(org_handle) do
    case get("pipeline_candidates:#{org_handle}") do
      %{"ids" => ids} when is_list(ids) -> ids
      _ -> nil
    end
  end

  def set_pipeline_candidates(org_handle, ids) when is_binary(org_handle) and is_list(ids) do
    set("pipeline_candidates:#{org_handle}", %{"ids" => ids})
  end

  @doc """
  Gets portfolio company IDs for a given host handle.
  Returns a list of organization IDs or an empty list if not set.
  """
  def get_portfolio_companies(host_handle) when is_binary(host_handle) do
    case get("portfolio_companies:#{host_handle}") do
      %{"org_ids" => org_ids} when is_list(org_ids) -> org_ids
      _ -> []
    end
  end

  @doc """
  Sets portfolio company IDs for a given host handle.
  """
  def set_portfolio_companies(host_handle, org_ids) when is_binary(host_handle) and is_list(org_ids) do
    set("portfolio_companies:#{host_handle}", %{"org_ids" => org_ids})
  end

  def get_org_feed(org_handle) when is_binary(org_handle) do
    case get("org_feed:#{org_handle}") do
      %{"entries" => entries} when is_list(entries) -> entries
      _ -> []
    end
  end

  def set_org_feed(org_handle, entries) when is_binary(org_handle) and is_list(entries) do
    set("org_feed:#{org_handle}", %{"entries" => entries})
  end

  def get_github_repo_allowlist do
    case get("github_repo_allowlist") do
      %{"owners" => owners} when is_list(owners) -> owners
      _ -> []
    end
  end

  def set_github_repo_allowlist(owners) when is_list(owners) do
    set("github_repo_allowlist", %{"owners" => owners})
  end

  defp format_timestamp(datetime) do
    datetime
    |> DateTime.to_string()
    |> String.replace(~r/\D/, "-")
    |> String.replace(~r/-+/, "-")
    |> String.trim_trailing("-")
  end
end
